plugins {
    id 'com.android.application'
}

// Keystore dosyasını yüklemek için
def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

// Sabit versiyon değerleri
def VERSION_CODE = 25
def VERSION_NAME = "2025.06.25.25"

android {
    namespace 'com.envanto.barcode'
    compileSdk 34

    defaultConfig {
        applicationId "com.envanto.barcode"
        minSdk 21
        targetSdk 34
        versionCode VERSION_CODE
        versionName VERSION_NAME
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        vectorDrawables.useSupportLibrary true
        
        // Uygulama yapılandırma değerleri
        buildConfigField "long", "UPLOADED_RECORDS_RETENTION_TIME", "43200000" // 12 saat (ms)
        buildConfigField "long", "UPLOAD_SERVICE_INTERVAL", "30000" // 30 saniye (ms)
        buildConfigField "int", "AUTO_REFRESH_INTERVAL", "10000" // 10 saniye (ms)
        buildConfigField "long", "UPDATE_CHECK_INTERVAL", "43200000" // 12 saat (ms) - APK güncelleme kontrol aralığı
        buildConfigField "long", "CUSTOMER_CACHE_UPDATE_INTERVAL", "3600000" // 1 saat (ms) - Müşteri listesi önbellek güncelleme
        buildConfigField "long", "SESSION_DURATION", "86400000" // 24 saat (ms) - Kullanıcı oturum süresi
        
        // API URL'leri
        buildConfigField "String", "API_BASE_URL", "\"https://envanto.app/barkod_yukle_android/\""
        buildConfigField "String", "APK_DOWNLOAD_BASE_URL", "\"https://envanto.app/barkod_yukle_android/apk/\""
        buildConfigField "String", "DEFAULT_BARCODE_URL_BASE", "\"https://envanto.app/barkodindex.asp?barcode=\""
        buildConfigField "String", "UPDATE_CHECK_URL", "\"https://envanto.app/barkod_yukle_android/updatecontrol.asp\""
        
        // UI ve UX Süre Sabitleri
        buildConfigField "int", "LOADING_DIALOG_DELAY", "500" // Yükleme dialog gecikmesi (ms)
        buildConfigField "int", "SHORT_DELAY", "1000" // Kısa gecikme süresi (ms)
        buildConfigField "int", "MEDIUM_DELAY", "2000" // Orta gecikme süresi (ms)
        buildConfigField "int", "NOTIFICATION_DISPLAY_DURATION", "10000" // Bildirim gösterim süresi (ms)
        
        // Kamera ve Tarama Süreleri
        buildConfigField "long", "AUTO_FOCUS_INTERVAL", "500" // Otomatik fokus aralığı (ms)
        buildConfigField "int", "SCAN_ANIMATION_DURATION", "2000" // Tarama animasyon süresi (ms)
        
        // Network ve API Timeout'ları
        buildConfigField "int", "QUICK_API_TIMEOUT", "3000" // Hızlı API işlemler için timeout (ms)
        buildConfigField "int", "THREAD_JOIN_TIMEOUT", "1000" // Thread join timeout (ms)
    }
    
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias'] ?: System.getenv("KEYSTORE_ALIAS")
            keyPassword keystoreProperties['keyPassword'] ?: System.getenv("KEYSTORE_PASSWORD")
            storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
            storePassword keystoreProperties['storePassword'] ?: System.getenv("STORE_PASSWORD")
        }
    }
    
    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    buildFeatures {
        viewBinding true
        buildConfig true
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/DEPENDENCIES', 'META-INF/LICENSE', 'META-INF/LICENSE.txt', 'META-INF/license.txt', 'META-INF/NOTICE', 'META-INF/NOTICE.txt', 'META-INF/notice.txt', 'META-INF/ASL2.0']
        }
    }


    lint {
        baseline = file("lint-baseline.xml")
    }
    
    // Özel APK dosya adı formatı
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "Envanto-Barcode-v.${variant.versionName}.apk"
        }
    }
}

// Java sürümünü zorla ayarla
tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:unchecked'
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    
    // ML Kit - Barkod tarama için
    implementation 'com.google.mlkit:barcode-scanning:17.3.0'
    
    // CameraX kütüphaneleri
    implementation 'androidx.camera:camera-camera2:1.4.2'
    implementation 'androidx.camera:camera-lifecycle:1.4.2'
    implementation 'androidx.camera:camera-view:1.4.2'
    
    // Chrome Custom Tabs
    implementation 'androidx.browser:browser:1.8.0'
    
    // Network işlemleri için
    implementation 'com.squareup.retrofit2:retrofit:3.0.0'
    implementation 'com.squareup.retrofit2:converter-gson:3.0.0'
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.12.0'
    
    // MySQL yerine Android uyumlu veritabanı bağlantısı için
    implementation('org.apache.commons:commons-dbcp2:2.13.0') {
        exclude group: 'commons-logging', module: 'commons-logging'
    }
    implementation 'net.sourceforge.jtds:jtds:1.3.1'
    
    // Testing
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
} 